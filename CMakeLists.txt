cmake_minimum_required(VERSION 3.1)
cmake_policy(VERSION 3.1)

# Enable policy to not use RPATH settings for install_name on macOS.
if(POLICY CMP0068)
  cmake_policy(SET CMP0068 NEW)
endif()

# Enable policy to run automoc on generated files.
if(POLICY CMP0071)
  cmake_policy(SET CMP0071 NEW)
endif()

# Consider changing the project name to something relevant for you.
project(QwtBinding)

# Activate CMake modules specific to this project
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/Modules/")

# ================================ General configuration ======================================

# Conditionals based off Debug/Release configuration
string(TOUPPER ${CMAKE_BUILD_TYPE} CMAKE_BUILD_TYPE)
if(${CMAKE_BUILD_TYPE} MATCHES DEBUG)
    message("Debug Mode")
    set(PYTHON_EXE "python_d")
    set(QWT_LIB "qwtd")
else()
    message("Release Mode")
    set(PYTHON_EXE "python")
    set(QWT_LIB "qwt")
endif()


# Set CPP standard to C++11 minimum.
set(CMAKE_CXX_STANDARD 11)

# Find QWT library
find_package(Qwt REQUIRED)

# This doesn't really do anything if qwt is built with no rpath support.
get_filename_component(qwt_rpath ${QWT_LIBRARY} NAME)

# The name of the generated bindings module (as imported in Python). You can change the name
# to something relevant for your project.
set(bindings_library "pysideqwt")

# The header file with all the types and functions for which bindings will be generated.
# Usually it simply includes other headers of the library you are creating bindings for.
set(wrapped_header ${CMAKE_SOURCE_DIR}/bindings.h)

# The typesystem xml file which defines the relationships between the C++ types / functions
# and the corresponding Python equivalents.
set(typesystem_file ${CMAKE_SOURCE_DIR}/bindings.xml)

# Specify which C++ files will be generated by shiboken. This includes the module wrapper
# and a '.cpp' file per C++ type. These are needed for generating the module shared
# library.
set(generated_sources
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/pysideqwt_module_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotcurve_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpickermachine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpickerclickpointmachine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpickerclickrectmachine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpickerdraglinemachine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpickerdragpointmachine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpickerdragrectmachine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpickerpolygonmachine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpickertrackermachine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwteventpattern_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpicker_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotcanvas_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotlayout_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotpicker_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotzoomer_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtpanner_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotpanner_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtlegenddata_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtabstractlegend_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtabstractseriesstore_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtlegend_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtabstractscaledraw_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtscaledraw_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplot_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotdict_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsymbol_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtcurvefitter_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplinecurvefitter_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtweedingcurvefitter_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwttransform_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtscalemap_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotitem_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotmarker_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotshapeitem_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotseriesitem_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwttext_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwttextlabel_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtscalediv_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtscaleengine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtscalewidget_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtlinearscaleengine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtdatescaleengine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtlogscaleengine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotgrid_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotcanvas_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplotrescaler_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtbezier_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwttextengine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtplaintextengine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtrichtextengine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtspline_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplinebasis_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplineinterpolating_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplineg1_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplinec1_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplinec2_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplinecubic_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplinelocal_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplineparametrization_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplinepleasing_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwtsplinepolynomial_wrapper.cpp
)

# ================================== Shiboken detection ======================================

# Find required Qt packages.
find_package(Qt5 5.12 REQUIRED COMPONENTS Core Gui Widgets)

# Macro to get various pyside / python include / link flags and paths.
# Uses the not entirely supported pyside2_config.py file.
macro(pyside2_config option output_var)
    if(${ARGC} GREATER 2)
        set(is_list ${ARGV2})
    else()
        set(is_list "")
    endif()

    execute_process(
      COMMAND ${PYTHON_EXE} "${CMAKE_SOURCE_DIR}/pyside2_config.py" ${option}
      OUTPUT_VARIABLE ${output_var}
      OUTPUT_STRIP_TRAILING_WHITESPACE)

    if ("${${output_var}}" STREQUAL "")
        message(FATAL_ERROR "Error: Calling pyside2_config.py ${option} returned no output.")
    endif()
    if(is_list)
        string (REPLACE " " ";" ${output_var} "${${output_var}}")
    endif()
endmacro()

# Query for the shiboken2-generator path, PySide2 path, Python path, include paths and linker flags.
pyside2_config(--shiboken2-module-path shiboken2_module_path)
pyside2_config(--shiboken2-generator-path shiboken2_generator_path)
pyside2_config(--pyside2-path pyside2_path)

pyside2_config(--python-include-path python_include_dir)
pyside2_config(--shiboken2-generator-include-path shiboken2_generator_include_dir 1)
pyside2_config(--pyside2-include-path pyside2_include_dir 1)

pyside2_config(--python-link-flags-cmake python_linking_data 0)
pyside2_config(--shiboken2-module-shared-libraries-cmake shiboken2_module_shared_libraries 0)
pyside2_config(--pyside2-shared-libraries-cmake pyside2_shared_libraries 0)

set(shiboken2_path "${shiboken2_generator_path}/shiboken2${CMAKE_EXECUTABLE_SUFFIX}")
if(NOT EXISTS ${shiboken2_path})
    message(FATAL_ERROR "Shiboken executable not found at path: ${shiboken2_path}")
endif()

# Get all relevant Qt include dirs, to pass them on to shiboken.
get_property(QT_CORE_INCLUDE_DIRS TARGET Qt5::Core PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_GUI_INCLUDE_DIRS TARGET Qt5::Gui PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_WIDGETS_INCLUDE_DIRS TARGET Qt5::Widgets PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
set(QT_INCLUDE_DIRS ${QT_CORE_INCLUDE_DIRS} ${QT_GUI_INCLUDE_DIRS} ${QT_WIDGETS_INCLUDE_DIRS})
set(INCLUDES "")
foreach(INCLUDE_DIR ${QT_INCLUDE_DIRS})
    list(APPEND INCLUDES "-I${INCLUDE_DIR}")
endforeach()

# On macOS, check if Qt is a framework build. This affects how include paths should be handled.
get_target_property(QtCore_is_framework Qt5::Core FRAMEWORK)
if (QtCore_is_framework)
    get_target_property(qt_core_library_location Qt5::Core LOCATION)
    get_filename_component(qt_core_library_location_dir "${qt_core_library_location}" DIRECTORY)
    get_filename_component(lib_dir "${qt_core_library_location_dir}/../" ABSOLUTE)
    list(APPEND INCLUDES "--framework-include-paths=${lib_dir}")
endif()

# ==================================== RPATH configuration ====================================


# =============================================================================================
# !!! (The section below is deployment related, so in a real world application you will want to
# take care of this properly with some custom script or tool).
# =============================================================================================
# Enable rpaths so that the built shared libraries find their dependencies.
set(CMAKE_MACOSX_RPATH 1)
set(CMAKE_SKIP_BUILD_RPATH FALSE)
set(CMAKE_BUILD_WITH_INSTALL_RPATH TRUE)
set(CMAKE_INSTALL_RPATH "@loader_path" ${pyside2_path} ${CMAKE_CURRENT_SOURCE_DIR} "@loader_path/${qwt_rpath}" ${shiboken2_module_path})
message("CMAKE_INSTALL_RPATH ${CMAKE_INSTALL_RPATH}")
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)

# =============================================================================================
# !!! End of dubious section.
# =============================================================================================


# ====================== Shiboken target for generating binding C++ files  ====================


# Set up the options to pass to shiboken.
set(shiboken_options --generator-set=shiboken --enable-parent-ctor-heuristic
    --enable-pyside-extensions --enable-return-value-heuristic
    --use-isnull-as-nb_nonzero --avoid-protected-hack
    ${INCLUDES}
    -I${CMAKE_SOURCE_DIR}
    -T${CMAKE_SOURCE_DIR}
    -T${pyside2_path}/typesystems
    -I${QWT_INCLUDE_DIR}
    --output-directory=${CMAKE_CURRENT_BINARY_DIR}
    )

set(generated_sources_dependencies ${wrapped_header} ${typesystem_file})

# Add custom target to run shiboken to generate the binding cpp files.
add_custom_command(OUTPUT ${generated_sources}
                    COMMAND ${shiboken2_path}
                    ${shiboken_options} ${wrapped_header} ${typesystem_file}
                    DEPENDS ${generated_sources_dependencies}
                    IMPLICIT_DEPENDS CXX ${wrapped_header}
                    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                    COMMENT "Running generator for ${typesystem_file}.")

# We need to include the headers for the module bindings that we use.
set(pyside2_additional_includes "")
foreach(include_dir ${pyside2_include_dir})
    list(APPEND pyside2_additional_includes "${include_dir}/QtCore")
    list(APPEND pyside2_additional_includes "${include_dir}/QtGui")
    list(APPEND pyside2_additional_includes "${include_dir}/QtWidgets")
endforeach()

# =============================== CMake target - bindings_library =============================


# Set the cpp files which will be used for the bindings library.
set(${bindings_library}_sources ${generated_sources})

# Define and build the bindings library.
add_library(${bindings_library} MODULE ${${bindings_library}_sources})

# Enable automoc.
set_property(TARGET ${bindings_library} PROPERTY AUTOMOC 1)

# Apply relevant include and link flags.
target_include_directories(${bindings_library} PRIVATE ${QWT_INCLUDE_DIR})
target_include_directories(${bindings_library} PRIVATE ${python_include_dir})
target_include_directories(${bindings_library} PRIVATE ${shiboken2_generator_include_dir})
target_include_directories(${bindings_library} PRIVATE ${pyside2_include_dir})
target_include_directories(${bindings_library} PRIVATE ${pyside2_additional_includes})
target_include_directories(${bindings_library} PRIVATE ${CMAKE_SOURCE_DIR})

target_link_libraries(${bindings_library} PRIVATE ${shiboken2_module_shared_libraries})
target_link_libraries(${bindings_library} PRIVATE ${QWT_LIBRARY})
target_link_libraries(${bindings_library} PRIVATE Qt5::Widgets)
target_link_libraries(${bindings_library} PRIVATE ${pyside2_shared_libraries})

# Adjust the name of generated module.
set_property(TARGET ${bindings_library} PROPERTY PREFIX "")
set_property(TARGET ${bindings_library} PROPERTY OUTPUT_NAME
             "${bindings_library}${PYTHON_EXTENSION_SUFFIX}")
if(WIN32)
    set_property(TARGET ${bindings_library} PROPERTY SUFFIX ".pyd")
    # Define QWT_DLL to mark QWT method prototypes as DLL imports.
    add_definitions(-DQWT_DLL)
endif()

# Make sure the linker doesn't complain about not finding Python symbols on macOS.
if(APPLE)
  set_target_properties(${bindings_library} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
endif(APPLE)

# Find and link to the python import library only on Windows.
# On Linux and macOS, the undefined symbols will get resolved by the dynamic linker
# (the symbols will be picked up in the Python executable).
if (WIN32)
    list(GET python_linking_data 0 python_libdir)
    list(GET python_linking_data 1 python_lib)
    find_library(python_link_flags ${python_lib} PATHS ${python_libdir} HINTS ${python_libdir})
    target_link_libraries(${bindings_library} PRIVATE ${python_link_flags})
endif()

# Same as CONFIG += no_keywords to avoid syntax errors in object.h due to the usage of the word Slot
target_compile_definitions(${bindings_library} PRIVATE QT_NO_KEYWORDS)


# ================================= Dubious deployment section ================================


if(WIN32)
    # =========================================================================================
    # !!! (The section below is deployment related, so in a real world application you will
    # want to take care of this properly (this is simply to eliminate errors that users usually
    # encounter.
    # =========================================================================================
    # Circumvent some "#pragma comment(lib)"s in "include/pyconfig.h" which might force to link
    # against a wrong python shared library.

    set(python_versions_list 3 32 33 34 35 36 37 38)
    set(python_additional_link_flags "")
    foreach(ver ${python_versions_list})
        set(python_additional_link_flags
            "${python_additional_link_flags} /NODEFAULTLIB:\"python${ver}_d.lib\"")
        set(python_additional_link_flags
            "${python_additional_link_flags} /NODEFAULTLIB:\"python${ver}.lib\"")
    endforeach()

    set_target_properties(${bindings_library}
                           PROPERTIES LINK_FLAGS "${python_additional_link_flags}")

    # Add custom target to copy pyside2 and shiboken shared libraries into the build folder,
    # so that the user doesn't have to set the PATH manually to point to the PySide2 package.
    set(shared_libraries ${shiboken2_module_shared_libraries} ${pyside2_shared_libraries})
    foreach(library_path ${shared_libraries})
        string(REGEX REPLACE ".lib$" ".dll" library_path ${library_path})
        get_filename_component(base_name ${library_path} NAME)
        file(TO_NATIVE_PATH ${library_path} source_path)
        file(TO_NATIVE_PATH "${CMAKE_CURRENT_BINARY_DIR}/${base_name}" dest_path)
        add_custom_command(OUTPUT "${base_name}"
                            COMMAND copy "${source_path}" "${dest_path}"
                            DEPENDS ${library_path}
                            WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
                            COMMENT "Copying PySide2 shared library ${base_name} to ${dest_path}")

        # Fake target that depends on the previous one, but has special ALL keyword, which means
        # it will always be executed.
        add_custom_target("fake_${base_name}" ALL DEPENDS ${base_name})
    endforeach()
    # =========================================================================================
    # !!! End of dubious section.
    # =========================================================================================
endif()

# =============================================================================================
# !!! (The section below is deployment related, so in a real world application you will want to
# take care of this properly with some custom script or tool).
# =============================================================================================
# Install the library and the bindings module into the source folder near the main.py file, so
# that the Python interpeter successfully imports the used module.
install(TARGETS ${bindings_library}
        LIBRARY DESTINATION ${CMAKE_CURRENT_SOURCE_DIR}/package/pysideqwt
        RUNTIME DESTINATION ${CMAKE_CURRENT_SOURCE_DIR}/package/pysideqwt
        )

# Qwt seems to be built with no rpath support, so to load the generated module, the framework
# has to be copied to the same folder as the .so file. 
if(APPLE)
   install(DIRECTORY "${QWT_FRAMEWORK}" DESTINATION ${CMAKE_CURRENT_SOURCE_DIR}/package/pysideqwt)
endif()

# =============================================================================================
# !!! End of dubious section.
# =============================================================================================
